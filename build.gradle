plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.16.0'
    id "io.freefair.lombok" version "4.1.2"
}


group 'cc.c1234'
version '0.1.8'

sourceCompatibility = 11

idea {
    module {
        inheritOutputDirs = true
    }
}
lombok {
    version = "1.18.10"
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base']
}

mainClassName = "PrettyZoo.main/cc.cc1234.main.PrettyZooApplication"
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    forceMerge("jackson", "log4j-api")
    launcher {
        name = 'prettyZoo'
    }
}

compileJava {
    inputs.property("moduleName", "PrettyZoo.main")
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    mavenCentral()
}

dependencies {
    compile('org.apache.curator:curator-recipes:4.2.0') {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }

    compile ('org.apache.zookeeper:zookeeper:3.4.6') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'io.netty', module: 'netty'
        exclude group: 'log4j', module: 'log4j'
    }

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'

    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.2'
    compile group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.3.1.Final'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
